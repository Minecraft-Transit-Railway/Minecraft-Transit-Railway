import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os

loom {
	forge {
		mixinConfig "mtr.mixins.json"
	}
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.forge_version}"
	modApi "${rootProject.architectury_id}:architectury-forge:${rootProject.architectury_data[0].split("\\+")[0]}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

shadowJar {
	exclude "fabric.mod.json"

	configurations = [project.configurations.shadowCommon]
	archiveClassifier = "dev-shadow"
	relocate "javax.servlet", "mtr.libraries.javax.servlet"
	relocate "org.eclipse", "mtr.libraries.org.eclipse"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier = null
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

tasks.register("moveForgeBuilds") {
	doLast {
		delete fileTree("../build/release").matching {
			include "**/${rootProject.variation_header}-forge*"
		}
		delete fileTree("../build/libs").matching {
			include "**/${rootProject.variation_header}-forge*"
		}
		delete fileTree("../build/latest").matching {
			include "**/${rootProject.variation_header}-forge*"
		}
		copy {
			from "build/libs"
			include "*${rootProject.mod_version}.jar"
			into "../build/release"
			rename "(.+)", "${rootProject.variation_header}-\$1"
		}
		if (rootProject.variation == "normal") {
			copy {
				from "build/libs"
				into "../build/latest"
				rename { fileName -> "MTR-${fileName.replace(rootProject.mod_version, "latest")}" }
			}
			copy {
				from "build/libs"
				include "*${rootProject.mod_version}-dev-shadow.jar"
				into "../build/libs"
				rename "(.+)", "MTR-\$1"
			}
		}
	}
}

tasks.register("testServer") {
	doLast {
		if (rootProject.variation == "normal") {
			copy {
				from "../resources/eula.txt"
				into "server"
			}
			copy {
				from "../resources/server.properties"
				into "server"
				filter(ReplaceTokens, tokens: ["port": String.valueOf(25500 + new Random().nextInt(500))])
			}
			copy {
				from "../build/latest"
				into "server/mods"
				include "MTR-forge-*-latest.jar"
				rename "(.+)", "MTR.jar"
			}
			download.run {
				src rootProject.architectury_data[1]
				dest "forge/server/mods/architectury.jar"
				overwrite true
				retries - 1
			}
			download.run {
				src "https://maven.minecraftforge.net/net/minecraftforge/forge/${rootProject.forge_version}/forge-${rootProject.forge_version}-installer.jar"
				dest "forge/server/forge-installer.jar"
				overwrite true
				retries - 1
			}
			javaexec {
				mainClass = "-jar"
				args = ["forge-installer.jar", "--installServer"]
				workingDir = "server"
			}
			if (!rootProject.forge_version.startsWith("1.16.5")) {
				javaexec {
					mainClass = "@libraries/net/minecraftforge/forge/${rootProject.forge_version}/${Os.isFamily(Os.FAMILY_WINDOWS) ? "win" : "unix"}_args.txt"
					args = ["nogui"]
					jvmArgs = ["-Xms4G", "-Xmx4G"]
					workingDir = "server"
				}
			}
		}
	}
}

afterEvaluate {
	for (def task in it.tasks) {
		if (task == tasks.build) {
			task.finalizedBy tasks.moveForgeBuilds
		}
	}
}
