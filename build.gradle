import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
	id "de.undercouch.download" version "4.1.2"
	id "com.github.johnrengelman.shadow" version "7.1.0"
}

def default_minecraft_version = "1.16.5"
def minecraft_version = rootProject.properties.containsKey("buildVersion") ? rootProject.getProperties().get("buildVersion") : default_minecraft_version
def is_1_16 = (minecraft_version as String).startsWith("1.16")
def is_1_17 = (minecraft_version as String).startsWith("1.17")
def is_1_18 = (minecraft_version as String) == "1.18"
def is_1_18_2 = (minecraft_version as String) == "1.18.2"
def patreon_api_key = rootProject.properties.containsKey("patreonApiKey") ? rootProject.getProperties().get("patreonApiKey") : ""

rootProject.ext.architectury_version = is_1_16 ? rootProject.architectury_version_1_16 : is_1_17 ? rootProject.architectury_version_1_17 : is_1_18 ? rootProject.architectury_version_1_18 : is_1_18_2 ? rootProject.architectury_version_1_18_2 : rootProject.architectury_version_1_19
rootProject.ext.architectury_id = is_1_16 ? "me.shedaniel" : "dev.architectury"
rootProject.ext.fabric_api_version = is_1_16 ? rootProject.fabric_version_1_16 : is_1_17 ? rootProject.fabric_version_1_17 : is_1_18 ? rootProject.fabric_version_1_18 : is_1_18_2 ? rootProject.fabric_version_1_18_2 : rootProject.fabric_version_1_19
rootProject.ext.forge_version = is_1_16 ? rootProject.forge_version_1_16 : is_1_17 ? rootProject.forge_version_1_17 : is_1_18 ? rootProject.forge_version_1_18 : is_1_18_2 ? rootProject.forge_version_1_18_2 : rootProject.forge_version_1_19
rootProject.ext.mod_menu_version = is_1_16 ? rootProject.mod_menu_version_1_16 : is_1_17 ? rootProject.mod_menu_version_1_17 : is_1_18 ? rootProject.mod_menu_version_1_18 : is_1_18_2 ? rootProject.mod_menu_version_1_18_2 : rootProject.mod_menu_version_1_19

architectury {
	minecraft = minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	configurations {
		shadowCommon
	}

	dependencies {
		minecraft "com.mojang:minecraft:${minecraft_version}"
		mappings loom.officialMojangMappings()
		implementation "us.dynmap:dynmap-api:${rootProject.dynmap_version}"
		implementation "com.github.BlueMap-Minecraft:BlueMapAPI:${rootProject.blue_map_version}"
		implementation "xyz.jpenilla:squaremap-api:${rootProject.squaremap_version}"
		implementation "org.eclipse.jetty:jetty-http:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-io:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-security:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-server:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-servlet:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-util:${rootProject.jetty_version}"
		implementation "org.eclipse.jetty:jetty-util-ajax:${rootProject.jetty_version}"
		implementation "javax.servlet:javax.servlet-api:${rootProject.servlet_api_version}"
		implementation "org.msgpack:msgpack-core:${rootProject.messagepack_version}"
		shadowCommon "org.eclipse.jetty:jetty-http:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-io:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-security:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-server:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-servlet:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-util:${rootProject.jetty_version}"
		shadowCommon "org.eclipse.jetty:jetty-util-ajax:${rootProject.jetty_version}"
		shadowCommon "javax.servlet:javax.servlet-api:${rootProject.servlet_api_version}"
		shadowCommon "org.msgpack:msgpack-core:${rootProject.messagepack_version}"
	}
}

task setupFiles() {
	download {
		src "https://github.com/jonafanho/Minecraft-Mappings/archive/refs/heads/${is_1_16 ? "1.16" : is_1_17 ? "1.17" : is_1_18 || is_1_18_2 ? "1.18" : "1.19"}.zip"
		dest "common/src/main/java/mtr/mappings/mappings.zip"
		overwrite true
	}
	copy {
		outputs.upToDateWhen { false }
		from(zipTree("common/src/main/java/mtr/mappings/mappings.zip")) { eachFile { file -> file.relativePath = new RelativePath(true, file.relativePath.segments.drop(1) as String[]) } }
		into "common/src/main/java/mtr/mappings"
		filter(ReplaceTokens, tokens: ["package": "mtr.mappings"])
	}
	ant.path { ant.fileset(dir: "common/src/main/java/mtr/mappings", includes: "Fabric*.java") }.list().each {
		ant.move(file: it, todir: "fabric/src/main/java/mtr/mappings")
	}
	ant.path { ant.fileset(dir: "common/src/main/java/mtr/mappings", includes: "Forge*.java") }.list().each {
		ant.move(file: it, todir: "forge/src/main/java/mtr/mappings")
	}

	download {
		src "https://github.com/dekimasoon/pixi-simple-gesture/archive/refs/heads/master.zip"
		dest "common/src/main/resources/assets/mtr/website/gestures/gestures.zip"
		overwrite true
	}
	copy {
		outputs.upToDateWhen { false }
		from(zipTree("common/src/main/resources/assets/mtr/website/gestures/gestures.zip")) { eachFile { file -> file.relativePath = new RelativePath(true, file.relativePath.segments.drop(1) as String[]) } }
		into "common/src/main/resources/assets/mtr/website/gestures"
	}

	download {
		src "https://crowdin.com/backend/download/project/minecraft-transit-railway.zip"
		dest "common/src/main/java/mtr/mappings/lang.zip"
		overwrite true
	}
	copy {
		outputs.upToDateWhen { false }
		from(zipTree("common/src/main/java/mtr/mappings/lang.zip"))
		into "common/src/main/resources/assets/mtr/lang"
	}

	copy {
		outputs.upToDateWhen { false }
		from "common/src/main/KeysTemplate.java"
		into "common/src/main/java/mtr"
		filter(ReplaceTokens, tokens: ["key": patreon_api_key, "version": minecraft_version + "-" + rootProject.mod_version])
		rename "(.+)Template.java", "\$1.java"
	}
	copy {
		outputs.upToDateWhen { false }
		from "common/src/main/version-template.js"
		into "common/src/main/resources/assets/mtr/website"
		filter(ReplaceTokens, tokens: ["version": minecraft_version + "-" + rootProject.mod_version])
		rename "(.+)-template.js", "\$1.js"
	}
	copy {
		outputs.upToDateWhen { false }
		from "fabric/src/main/UnknownPacketMixinTemplate.java"
		into "fabric/src/main/java/mtr/mixin"
		filter(ReplaceTokens, tokens: ["path1": is_1_16 || is_1_17 || is_1_18 ? "apache/logging/log4j" : "slf4j", "path2": is_1_16 || is_1_17 || is_1_18 ? "apache.logging.log4j" : "slf4j"])
		rename "(.+)Template.java", "\$1.java"
	}
	if (is_1_16 || is_1_17) {
		copy {
			outputs.upToDateWhen { false }
			from "fabric/src/main/BlockEntityExtensionTemplate.java"
			into "fabric/src/main/java/" + (is_1_16 ? "me/shedaniel" : "dev") + "/architectury/extensions"
			filter(ReplaceTokens, tokens: ["package": is_1_16 ? "me.shedaniel" : "dev"])
			rename "(.+)Template.java", "\$1.java"
		}
	}
	if (is_1_16 || is_1_17 || is_1_18 || is_1_18_2) {
		copy {
			outputs.upToDateWhen { false }
			from "common/src/main/RandomSourceTemplate.java"
			into "common/src/main/java/net/minecraft/util"
			rename "(.+)Template.java", "\$1.java"
		}
	}

	if (!is_1_16) {
		ant.delete(dir: "fabric/src/main/java/me")
	}
	if (!is_1_17) {
		ant.delete(dir: "fabric/src/main/java/dev")
	}
	if (!is_1_16 && !is_1_17 && !is_1_18 && !is_1_18_2) {
		ant.delete(dir: "common/src/main/java/net")
	}
}

allprojects {
	apply plugin: "architectury-plugin"

	version = minecraft_version + "-" + rootProject.mod_version
	group = rootProject.maven_group

	repositories {
		maven { url = "https://jitpack.io/" }
		maven { url = "https://maven.terraformersmc.com/" }
		maven { url = "https://repo.mikeprimm.com/" }
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"

		if (is_1_16) {
			def targetVersion = 8
			if (JavaVersion.current().isJava9Compatible()) {
				options.release = targetVersion
			}
		} else if (is_1_17) {
			options.release = 16
		} else {
			options.release = 17
		}
	}

	afterEvaluate {
		for (def task in it.tasks) {
			if (task != rootProject.tasks.setupFiles) {
				task.dependsOn rootProject.tasks.setupFiles
			}
		}
	}
}
