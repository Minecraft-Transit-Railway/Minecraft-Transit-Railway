plugins {
	id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "com.github.johnrengelman.shadow" version "8.1.1" apply false
}

architectury {
	minecraft = minecraft_version
}

allprojects {
	group = maven_group
	version = mod_version
}

subprojects {
	apply plugin: "dev.architectury.loom"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	base {
		// Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
		archivesName = "$archives_name-$project.name"
	}

	repositories {
		flatDir { dirs "../libs" }
		maven { url "https://repo.codemc.org/repository/maven-public" }
		maven { url = "https://maven.terraformersmc.com/" }
	}

	dependencies {
		minecraft "net.minecraft:minecraft:$minecraft_version"
		mappings loom.layered {
			it.mappings("net.fabricmc:yarn:$yarn_mappings:v2")
			it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$yarn_mappings_patch_neoforge_version")
		}
		implementation "com.google.code.findbugs:jsr305:+"
		implementation "org.mtr:Shadow-Libraries-net:0.0.1"
		implementation "org.mtr:Shadow-Libraries-util:0.0.1"
		implementation "org.mtr:Transport-Simulation-Core:0.0.1"
		implementation "com.logisticscraft:occlusionculling:+"
		implementation "xyz.jpenilla:squaremap-api:+"
		implementation "com.test:Dynmap:3.2.1"
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}

	// Configure Maven publishing.
	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = base.archivesName.get()
				from components.java
			}
		}

		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		repositories {
			// Add repositories to publish to here.
			// Notice: This block does NOT have the same function as the block in the top level.
			// The repositories here will be used for publishing your artifact, not for
			// retrieving dependencies.
		}
	}
}
