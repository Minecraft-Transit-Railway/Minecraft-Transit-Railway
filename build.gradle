plugins {
	id "java"
	id "com.github.johnrengelman.shadow" version "+" apply false
}

group "org.mtr.mod"
version rootProject.properties.version

subprojects {
	apply plugin: "java"
	apply plugin: "com.github.johnrengelman.shadow"

	configurations {
		shadowImplementation
		implementation.extendsFrom shadowImplementation
	}

	dependencies {
		shadowImplementation "com.github.jonafanho:Shadow-Libraries:master-SNAPSHOT"
		shadowImplementation "org.mtr:Minecraft-Mappings-common:0.0.1"
		shadowImplementation "org.mtr:Transport-Simulation-Core:0.0.1"
		shadowImplementation "com.squareup.okhttp3:okhttp:+"
		shadowImplementation "org.eclipse.jetty:jetty-server:9.+"
		shadowImplementation "org.eclipse.jetty:jetty-servlet:9.+"
		shadowImplementation "org.eclipse.jetty:jetty-util:9.+"
		shadowImplementation "javax.servlet:javax.servlet-api:+"
		shadowImplementation "com.logisticscraft:occlusionculling:+"
		compileOnly "com.google.code.findbugs:jsr305:+"
		compileOnly "xyz.jpenilla:squaremap-api:+"
		compileOnly "net.kyori:adventure-api:+"
		compileOnly "com.test:Dynmap:3.2.1"
	}

	repositories {
		flatDir { dirs "../libs" }
		maven { url "https://jitpack.io" }
		maven { url "https://repo.codemc.org/repository/maven-public" }
	}

	shadowJar {
		configurations = [project.configurations.shadowImplementation]
		minimize {
			exclude(dependency("com.github.jonafanho.Shadow-Libraries:net:.*"))
			exclude(dependency("org.mtr.*:.*:.*"))
		}
		relocate "com.logisticscraft", "org.mtr.libraries.com.logisticscraft"
		relocate "org.eclipse", "org.mtr.libraries.org.eclipse"
		relocate "javax.servlet", "org.mtr.libraries.javax.servlet"
	}

	tasks.withType(Javadoc).tap {
		configureEach { javadoc.options.addStringOption("Xdoclint:none", "-quiet") }
	}
}
